// Persistence of Vision Ray Tracer Scene Description File
// File: ?.pov
// Vers: 3.5
// Desc: LightsysIV Skylight Scene Example
// Date: mm/dd/yy
// Auth: ?
//

#version 3.5;

#include "colors.inc"
#include "CIE.inc"
#include "lightsys.inc"
#include "lightsys_constants.inc"

global_settings {
  assumed_gamma 1.0
  radiosity { 
    recursion_limit 1 
    count 50 
    error_bound 0.2 
  }
}

#default {finish {ambient 0 diffuse 1}}

// -----------------------------------------------------
CIE_ColorSystemWhitepoint(Beta_ColSys, Daylight2Whitepoint(Kt_Daylight_Film))
CIE_GamutMapping(off)

// -----------------------------------------------------
#declare Lightsys_Brightness = 1.0;
#declare Lightsys_Filter = <1,1,1>;

// -----------------------------------------------------
#declare Al=18;    // sun altitude
#declare Az=205;   // sun rotation 
#declare North=-z;   
#declare DomeSize=1e5;
#declare Current_Turbidity = 5.0;
#declare Intensity_Mult = 0.7; 

#include "CIE_Skylight.inc"

// -----------------------------------------------------
camera {
  location  <0.0, 0.5, -6.0>
  direction 1.0*z
  right     x*image_width/image_height
  look_at   <0.0, 0.5,  0.0>
}

light_source{ 0
  Light_Color(SunColor,1)
  translate SolarPosition
}

// ----------------------------------------
plane {
  y, -1
  pigment { color ReferenceRGB(<0.9,0.8,0.3>) }
  normal { gradient z sine_wave turbulence 0.5 scale 3 }
}

sphere {
  0.0, 1
  texture {
    pigment {
      radial
      frequency 2
      color_map {
        [0.00 color ReferenceRGB(Red)]    [0.25 color ReferenceRGB(Red)]
        [0.25 color ReferenceRGB(Green)]  [0.50 color ReferenceRGB(Green)]
        [0.50 color ReferenceRGB(Blue)]   [0.75 color ReferenceRGB(Blue)]
        [0.75 color ReferenceRGB(Yellow)] [1.00 color ReferenceRGB(Yellow)]
      }
    }
    finish { specular 0.6 }
  }
  rotate <-40, 30, 0>
}
